#
# @Cache-Control: no-store
# @infer true
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>
PREFIX screener: <https://probabilitytrading.net/screener/2014/schema.ttl#>

SELECT ?iri ?label ?profile
        (group_concat(?com) AS ?comment)
        (group_concat(?cor) AS ?correlated)
        (group_concat(?of) AS ?ofExchange)
        (group_concat(?includeSector; separator = "\t") AS ?includeSectors)
        (group_concat(?includeIndustry; separator = "\t") AS ?includeIndustries)
        (group_concat(?includeCountry; separator = "\t") AS ?includeCountries)
        (max(?min) AS ?mincap) (max(?max) AS ?maxcap)
        (group_concat(?exclude) AS ?excludes)
        (group_concat(?include) AS ?includes) {
    ?p foaf:name ?profile ;
        foaf:member <$user> ;
        calli:hasComponent ?iri .
    ?iri a screener:SecurityClass;
        rdfs:label ?label .
    {
        ?iri rdfs:comment ?com
    } UNION {
        ?iri screener:correlated ?cor
    } UNION {
        ?iri screener:ofExchange ?of
    } UNION {
        ?iri screener:includeSector ?includeSector
    } UNION {
        ?iri screener:includeIndustry ?includeIndustry
    } UNION {
        ?iri screener:includeCountry ?includeCountry
    } UNION {
        ?iri screener:mincap ?min
    } UNION {
        ?iri screener:maxcap ?max
    } UNION {
        ?iri screener:exclude ?exclude
    } UNION {
        ?iri screener:include ?include
    }
} GROUP BY ?iri ?label ?profile HAVING (COUNT(?iri) > 0)
