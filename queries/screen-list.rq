#
# @Cache-Control: no-store
# @infer true
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>
PREFIX screener: <https://probabilitytrading.net/screener/2014/schema.ttl#>

SELECT ?iri ?label ?comment ?profile ?lookback ?forSecurity
        ?criteria ?criteria_label ?criteria_comment
        ?forIndicator ?differenceFrom ?percentOf
        ?forWatchIndicator ?differenceFromWatch ?percentOfWatch
        ?againstCorrelated ?lower ?upper ?weight
        ?gainIntercept ?gainSlope ?painIntercept ?painSlope {
    ?p foaf:member <$user> ;
        calli:hasComponent ?iri .
    ?iri a screener:Screen
    {
        ?iri rdfs:label ?label
    } UNION {
        ?iri rdfs:comment ?comment
    } UNION {
        ?p foaf:name ?profile
    } UNION {
        ?iri screener:lookback ?lookback
    } UNION {
        SELECT ?iri (group_concat(?security) AS ?forSecurity) {
            ?p foaf:member <$user> ;
                calli:hasComponent ?iri .
            ?iri screener:forSecurity ?security
        } GROUP BY ?iri
    } UNION {
        SELECT ?iri ?criteria ?criteria_label ?criteria_comment
                ?forIndicator ?differenceFrom ?percentOf
                ?forWatchIndicator ?differenceFromWatch ?percentOfWatch
                ?againstCorrelated ?lower ?upper ?weight
                ?gainIntercept ?gainSlope ?painIntercept ?painSlope {
            ?p foaf:member <$user> ;
                calli:hasComponent ?iri .
            ?iri screener:hasCriteria ?criteria
            OPTIONAL {
                ?criteria rdfs:label ?criteria_label
            } OPTIONAL {
                ?criteria rdfs:comment ?criteria_comment
            } OPTIONAL {
                ?criteria screener:forIndicator ?forIndicator
            } OPTIONAL {
                ?criteria screener:forWatchIndicator ?forWatchIndicator
            } OPTIONAL {
                ?criteria screener:differenceFrom ?differenceFrom
            } OPTIONAL {
                ?criteria screener:differenceFromWatch ?differenceFromWatch
            } OPTIONAL {
                ?criteria screener:percentOf ?percentOf
            } OPTIONAL {
                ?criteria screener:percentOfWatch ?percentOfWatch
            } OPTIONAL {
                ?criteria screener:againstCorrelated ?againstCorrelated
            } OPTIONAL {
                ?criteria screener:lower ?lower
            } OPTIONAL {
                ?criteria screener:upper ?upper
            } OPTIONAL {
                ?criteria screener:gainIntercept ?gainIntercept
            } OPTIONAL {
                ?criteria screener:gainSlope ?gainSlope
            } OPTIONAL {
                ?criteria screener:painIntercept ?painIntercept
            } OPTIONAL {
                ?criteria screener:painSlope ?painSlope
            } OPTIONAL {
                ?criteria screener:weight ?weight
            }
        }
    }
} ORDER BY ?label
