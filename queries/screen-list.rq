#
# @Cache-Control: no-store
# @infer true
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>
PREFIX screener: <https://probabilitytrading.net/screener/2014/schema.ttl#>

SELECT ?iri ?label ?profile ?hasWatchCriteria ?hasHoldCriteria ?forIndicator ?lower ?upper ?hasChangeReference {
    ?p foaf:name ?profile ;
        foaf:member <$user> ;
        calli:hasComponent ?iri .
    ?iri a screener:Screen;
        rdfs:label ?label .
    {
        ?iri screener:hasWatchCriteria ?hasWatchCriteria .
        ?hasWatchCriteria screener:forIndicator ?forIndicator
        OPTIONAL {
            ?hasWatchCriteria screener:lower ?lower
        } OPTIONAL {
            ?hasWatchCriteria screener:upper ?upper
        }
    } UNION {
        ?iri screener:hasHoldCriteria ?hasHoldCriteria .
        ?hasHoldCriteria screener:forIndicator ?forIndicator
        OPTIONAL {
            ?hasHoldCriteria screener:lower ?lower
        } OPTIONAL {
            ?hasHoldCriteria screener:upper ?upper
        }
    }
}
